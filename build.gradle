plugins {
    id 'idea'
    id 'java'
    id 'java-library-distribution'
    id 'maven'
    id 'signing'

    id 'org.gradle.java.experimental-jigsaw' version '0.1.1'
    id 'com.github.hierynomus.license' version '0.11.0'
}

apply from: 'gradle/config.gradle'

group config.group
version config.version
archivesBaseName = config.artifact

javaModule.name = "$group.$archivesBaseName"

sourceCompatibility = 9

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
   classifier = 'javadoc'
   from javadoc
}

task sourcesJar(type: Jar) {
   classifier = 'sources'
   from sourceSets.main.allSource
}

javadoc {
    /* Add support for tasty Java 8 tags */
    options.tags = [
            'apiNote:a:API Note:',
            'implSpec:a:Implementation Requirements:',
            'implNote:a:Implementation Note:'
    ]
}

license {
    header = rootProject.file('gradle/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year = config.year
    ext.author = config.author
    ext.project = config.name
}

artifacts {
   archives javadocJar, sourcesJar
}

signing {
   sign configurations.archives
}

uploadArchives {
   repositories {
      mavenDeployer {
         beforeDeployment {
            MavenDeployment deployment -> signing.signPom(deployment)
         }
         
         repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: ossrhUserName, password: ossrhPassword)
         }
         
         snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: ossrhUserName, password: ossrhPassword)
         }
         
         pom.project {
            name config.name
            packaging 'jar'
            artifactId config.artifact
            description config.description
            url config.github_url
            
            scm {
               connection "scm:git:$config.github_link"
               developerConnection config.scm_link
               url config.github_link
            }
            
            licenses {
               license {
                  name 'The MIT License (MIT)'
                  url 'http://opensource.org/licenses/MIT'
                  distribution 'repo'
               }
            }
            
            developers {
               developer {
                  id 'jmathewburns'
                  name 'Jacques Burns'
                  email 'dev@rangefun.xyz'
               }
            }
         }
      }
   }
}